buildscript {
    ext.kotlin_version = '2.0.21'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.11.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'

// Write all build outputs to a spacious volume to avoid
// running out of space on the root filesystem during CI/local builds.
def propOrNull(String name) {
    if (project.hasProperty(name)) {
        def value = project.property(name)?.toString()
        if (value != null && !value.trim().isEmpty()) {
            return value.trim()
        }
    }
    return null
}

def propOrDefault(String name, def fallback) {
    return propOrNull(name) ?: fallback
}

def propAsInt(String name, int fallback) {
    def value = propOrNull(name)
    return value ? value.toInteger() : fallback
}

// Default to the roomy mount, allow override via -Popendroidpdf.buildDir
buildDir = propOrDefault("opendroidpdf.buildDir", "/mnt/subtitled/opendroidpdf-android-build")

repositories {
    google()
    mavenCentral()
}

def enablePdfBoxOps = (propOrNull("opendroidpdf.enablePdfBoxOps") ?: "false").toBoolean()
def enablePdfBoxOpsTest = (propOrNull("opendroidpdf.enablePdfBoxOpsTest") ?: "false").toBoolean()
def enableQpdfRegression = (propOrNull("qpdf.regression") ?: "false").toBoolean()
def enableQpdfOps = (propOrNull("opendroidpdf.enableQpdfOps") ?: "true").toBoolean()

def resolveAbiFilters() {
    def override = propOrNull("opendroidpdfAbi") ?: propOrNull("opendroidpdf.abi")
    if (override instanceof String && !override.trim().isEmpty()) {
        return override.split(",")
                .collect { it.trim() }
                .findAll { !it.isEmpty() }
    }
    return ['arm64-v8a', 'armeabi-v7a']
}

def resolvedAbiFilters = resolveAbiFilters()
def enableAbiSplits = (propOrNull("opendroidpdf.enableAbiSplits") ?: "false").toBoolean()
def qpdfSupportedAbis = ['arm64-v8a', 'armeabi-v7a']
def qpdfAbiCompatible = resolvedAbiFilters.any { it in qpdfSupportedAbis }
def enableQpdfOpsEffective = enableQpdfOps && qpdfAbiCompatible

android {
    namespace = propOrDefault('opendroidpdf.namespace', 'org.opendroidpdf')
    compileSdkVersion propAsInt('opendroidpdf.compileSdk', 36)
    buildToolsVersion propOrDefault('opendroidpdf.buildTools', "36.0.0")

    defaultConfig {
        applicationId propOrDefault('opendroidpdf.applicationId', 'org.opendroidpdf')
        minSdkVersion propAsInt('opendroidpdf.minSdk', 21)
        targetSdkVersion propAsInt('opendroidpdf.targetSdk', 36)
        versionCode propAsInt('opendroidpdf.versionCode', 106)
        versionName propOrDefault('opendroidpdf.versionName', '1.3.45')

        // qpdf JNI libraries are only shipped for the supported ABIs. Keep the feature
        // disabled for other ABI-only builds (e.g., CI x86_64 emulators) unless the
        // required native libs are added.
        buildConfigField "boolean", "ENABLE_QPDF_OPS", enableQpdfOpsEffective ? "true" : "false"
        // Allow enabling PDFBox only for tests without bloating the main APK.
        buildConfigField "boolean", "ENABLE_PDFBOX_OPS", (enablePdfBoxOps || enablePdfBoxOpsTest) ? "true" : "false"
        buildConfigField "boolean", "ENABLE_QPDF_REGRESSION", enableQpdfRegression ? "true" : "false"

        // When ABI splits are enabled, configure ABIs only via splits.abi.include
        // to avoid AGP's conflicting configuration error.
        if (!enableAbiSplits) {
            ndk {
                abiFilters.clear()
                abiFilters.addAll(resolvedAbiFilters)
            }
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments["qpdf.regression"] = enableQpdfRegression ? "true" : "false"
    }

    splits {
        abi {
            enable enableAbiSplits
            reset()
            include(*resolvedAbiFilters)
            // Keep F-Droid/release scripts expecting a single APK by default.
            universalApk false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources = false
        }
        release {
            minifyEnabled true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java {
                srcDirs = ['src']
                // Ensure local unit tests under src/test/** are not compiled into main.
                exclude 'test/**'
            }
            res.srcDirs = ['res']
            jniLibs.srcDirs = ['libs']
        }
        test {
            java.srcDirs = ['src/test/java']
        }
        androidTest {
            java.srcDirs = ['tests/androidTest/java']
            assets.srcDirs = ['tests/androidTest/assets', '../../test_assets/qpdf']
        }
    }

    externalNativeBuild {
        ndkBuild {
            path file('jni/Android.mk')
        }
    }

    ndkVersion = propOrDefault('opendroidpdf.ndkVersion', "27.2.12479018")

    packagingOptions {
        resources {
            excludes += [
                'META-INF/DEPENDENCIES',
                'META-INF/LICENSE',
                'META-INF/LICENSE.txt',
                'META-INF/license.txt',
                'META-INF/NOTICE',
                'META-INF/NOTICE.txt',
                'META-INF/notice.txt'
            ]
        }
    }
}

dependencies {
    implementation project(':core')
    implementation project(':officepack_api')
    implementation project(':xfapack_api')
    // Keep PDFBox optional: compileOnly for references, and conditionally bundle
    // when -Popendroidpdf.enablePdfBoxOps=true is supplied.
    compileOnly project(':pdfboxops')
    if (enablePdfBoxOps) {
        implementation project(':pdfboxops')
    }
    if (enablePdfBoxOpsTest) {
        androidTestImplementation project(':pdfboxops')
    }
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.9.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.6'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.core:core:1.13.1'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.viewpager:viewpager:1.0.0'
    implementation 'com.google.android.material:material:1.12.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.5.1'
}

// Helper to expose the resolved version to scripts in a single place.
tasks.register('printAppVersion') {
    doLast {
        println "versionCode=${android.defaultConfig.versionCode}"
        println "versionName=${android.defaultConfig.versionName}"
    }
}

// Helper to expose release/build settings to scripts in a single place.
tasks.register('printAppConfig') {
    doLast {
        def abiFilters = android.defaultConfig.ndk?.abiFilters
        println "applicationId=${android.defaultConfig.applicationId}"
        println "versionCode=${android.defaultConfig.versionCode}"
        println "versionName=${android.defaultConfig.versionName}"
        println "buildDir=${project.buildDir.absolutePath}"
        println "abiFilters=${abiFilters ? abiFilters.join(',') : ''}"
    }
}

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.11.0'
    }
}

apply plugin: 'com.android.application'

// Write all build outputs to a spacious volume to avoid
// running out of space on the root filesystem during CI/local builds.
buildDir = "/mnt/subtitled/penandpdf-android-build"

repositories {
    google()
    mavenCentral()
}

def resolveAbiFilters() {
    def override = project.findProperty("penandpdfAbi")
    if (override instanceof String && !override.trim().isEmpty()) {
        return override.split(",")
                .collect { it.trim() }
                .findAll { !it.isEmpty() }
    }
    return ['arm64-v8a', 'armeabi-v7a']
}

android {
    namespace 'org.opendroidpdf'
    compileSdkVersion 36
    buildToolsVersion "36.0.0"

    defaultConfig {
        applicationId 'org.opendroidpdf'
        minSdkVersion 21
       targetSdkVersion 36
        versionCode 92
        versionName '1.3.31'

        ndk {
            abiFilters.clear()
            abiFilters.addAll(resolveAbiFilters())
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        buildConfig true
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            jniLibs.srcDirs = ['libs']
        }
        androidTest {
            java.srcDirs = ['tests/androidTest/java']
            assets.srcDirs = ['tests/androidTest/assets']
        }
    }

    externalNativeBuild {
        ndkBuild {
            path file('jni/Android.mk')
        }
    }

    ndkVersion "27.2.12479018"

    packagingOptions {
        resources {
            excludes += [
                'META-INF/DEPENDENCIES',
                'META-INF/LICENSE',
                'META-INF/LICENSE.txt',
                'META-INF/license.txt',
                'META-INF/NOTICE',
                'META-INF/NOTICE.txt',
                'META-INF/notice.txt'
            ]
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.core:core:1.13.1'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.viewpager:viewpager:1.0.0'
    implementation 'com.google.android.material:material:1.12.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'junit:junit:4.13.2'
}

plugins {
    id 'com.android.library'
}

apply from: rootProject.file('gradle/core-sources.gradle')

def propOrNull(String name) {
    if (project.hasProperty(name)) {
        def value = project.property(name)?.toString()
        if (value != null && !value.trim().isEmpty()) {
            return value.trim()
        }
    }
    return null
}

def propOrDefault(String name, def fallback) {
    return propOrNull(name) ?: fallback
}

def propAsInt(String name, int fallback) {
    def value = propOrNull(name)
    return value ? value.toInteger() : fallback
}

repositories {
    google()
    mavenCentral()
}

// Reuse the roomy build dir if provided, but keep it namespaced per-module to
// avoid clobbering the app's outputs.
def sharedBuildRoot = propOrNull("opendroidpdf.buildDir")
if (sharedBuildRoot) {
    buildDir = "${sharedBuildRoot}/core"
}

android {
    namespace = 'org.opendroidpdf.core'
    compileSdkVersion propAsInt('opendroidpdf.compileSdk', 36)
    buildToolsVersion propOrDefault('opendroidpdf.buildTools', "36.0.0")

    defaultConfig {
        minSdkVersion propAsInt('opendroidpdf.minSdk', 21)
        targetSdkVersion propAsInt('opendroidpdf.targetSdk', 36)
        consumerProguardFiles 'proguard-rules.pro'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java {
                // Compile only the plain-Java shared types that are excluded
                // from the app module; everything else (especially Kotlin
                // controllers) stays in :app to avoid duplicate classes.
                // Include both the app's legacy tree (../src) and the core
                // module's own Java sources so we can gradually relocate
                // classes into core/src/main/java.
                srcDirs = ['../src', 'src/main/java']
                setIncludes(coreSourcePatterns)
            }
            // Keep Kotlin empty so the AAR cannot emit any of the appâ€™s
            // coroutine helpers or controllers twice.
            kotlin.srcDirs = []
            res.srcDirs = []
            assets.srcDirs = []
        }
    }
}

dependencies {
}

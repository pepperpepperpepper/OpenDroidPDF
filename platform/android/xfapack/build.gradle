plugins {
    id 'com.android.application'
}

def propOrNull(String name) {
    if (project.hasProperty(name)) {
        def value = project.property(name)?.toString()
        if (value != null && !value.trim().isEmpty()) {
            return value.trim()
        }
    }
    return null
}

def propOrDefault(String name, def fallback) {
    return propOrNull(name) ?: fallback
}

def propAsInt(String name, int fallback) {
    def value = propOrNull(name)
    return value ? value.toInteger() : fallback
}

repositories {
    google()
    mavenCentral()
}

// Reuse the roomy build dir if provided, but keep it namespaced per-module to
// avoid clobbering the app's outputs.
def sharedBuildRoot = propOrNull("opendroidpdf.buildDir")
if (sharedBuildRoot) {
    buildDir = "${sharedBuildRoot}/xfapack"
}

android {
    namespace = 'org.opendroidpdf.xfapack'
    compileSdkVersion propAsInt('opendroidpdf.compileSdk', 36)
    buildToolsVersion propOrDefault('opendroidpdf.buildTools', "36.0.0")

    defaultConfig {
        applicationId propOrDefault('opendroidpdf.xfapack.applicationId', 'org.opendroidpdf.xfapack')
        minSdkVersion propAsInt('opendroidpdf.minSdk', 21)
        targetSdkVersion propAsInt('opendroidpdf.targetSdk', 36)
        // Keep versioning in lockstep with the main app for easier distribution.
        versionCode propAsInt('opendroidpdf.versionCode', 106)
        versionName propOrDefault('opendroidpdf.versionName', '1.3.45')
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources = false
        }
        release {
            minifyEnabled true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            res.srcDirs = ['src/main/res']
            assets.srcDirs = []
        }
    }
}

dependencies {
    implementation project(':xfapack_api')
    implementation 'com.tom-roush:pdfbox-android:2.0.27.0'
}

// Helper to expose XFA Pack config to deployment scripts.
tasks.register('printXfaPackConfig') {
    doLast {
        println "applicationId=${android.defaultConfig.applicationId}"
        println "versionCode=${android.defaultConfig.versionCode}"
        println "versionName=${android.defaultConfig.versionName}"
        println "buildDir=${project.buildDir.absolutePath}"
    }
}
